/* ************ ECL CENOTES ************ */
/*
 * Cenotes maps for the ECL, used in the Americas region.
 * Based on the Cenotes map by Forgotten Empires.
 */


/* Sling nerf constants. */
#const TRIBUTE_INEFFICIENCY 46
#const BANKING 17
#const COINAGE 23
#const COINAGE_IMP_DISABLER 224


<PLAYER_SETUP>

random_placement
ai_info_map_type ARABIA 0 0 0

/* Sling nerfs */
effect_percent MOD_RESOURCE TRIBUTE_INEFFICIENCY ATTR_SET 40
effect_amount DISABLE_TECH BANKING ATTR_DISABLE 17
effect_amount DISABLE_TECH COINAGE ATTR_DISABLE 23
effect_amount DISABLE_TECH COINAGE_IMP_DISABLER ATTR_DISABLE 224


<LAND_GENERATION>
base_terrain GRASS2

create_player_lands {
  terrain_type GRASS
  land_percent 1
  base_size 18
  border_fuzziness 15
}


<TERRAIN_GENERATION>

create_terrain WATER {
  base_terrain GRASS2
  spacing_to_other_terrain_types 3
  if TINY_MAP
    number_of_tiles 800
  elseif SMALL_MAP
    number_of_tiles 1200
  elseif MEDIUM_MAP
    number_of_tiles 1600
  elseif LARGE_MAP
    number_of_tiles 2400
  elseif HUGE_MAP
    number_of_tiles 3200
  elseif GIGANTIC_MAP
    number_of_tiles 3200
  else
    number_of_tiles 5600
  endif
  if TINY_MAP
    number_of_clumps 6
  elseif SMALL_MAP
    number_of_clumps 9
  elseif MEDIUM_MAP
    number_of_clumps 12
  elseif LARGE_MAP
    number_of_clumps 16
  elseif HUGE_MAP
    number_of_clumps 22
  elseif GIGANTIC_MAP
    number_of_clumps 22
  else
    number_of_clumps 36
  endif
	clumping_factor 100
  set_avoid_player_start_areas
}

create_terrain SHALLOW {
  base_terrain                   WATER
  land_percent 3
  number_of_clumps 99
  set_avoid_player_start_areas
}

create_terrain JUNGLE {
  base_terrain                   GRASS2
  spacing_to_other_terrain_types 5
  land_percent                   8
  number_of_clumps               15
  set_avoid_player_start_areas
  set_scale_by_groups
}

create_terrain LEAVES {
  base_terrain                   GRASS2
  land_percent                   9
  number_of_clumps               26
  spacing_to_other_terrain_types 1
  set_scale_by_size
}

/* BASE FOREST */

create_terrain JUNGLE {
  base_terrain                   GRASS
  spacing_to_other_terrain_types 2
  if TINY_MAP
    number_of_clumps               9
    land_percent                   3
  elseif SMALL_MAP
    number_of_clumps               12
    land_percent                   3
  elseif MEDIUM_MAP
    number_of_clumps               18
    land_percent                   3
  elseif LARGE_MAP
    number_of_clumps               24
    land_percent                   3
  elseif HUGE_MAP
    number_of_clumps               30
    land_percent                   3
  elseif GIGANTIC_MAP
    number_of_clumps               30
    land_percent                   3
  endif
  set_avoid_player_start_areas
}

create_terrain GRASS2 {
  base_terrain                   GRASS
  number_of_clumps		15
  land_percent                   100
}

create_terrain GRASS2 {
  base_terrain                   GRASS
  number_of_clumps		15
  land_percent                   100
}

create_terrain GRASS2 {
  base_terrain                   GRASS
  number_of_clumps		15
  land_percent                   100
}

create_terrain GRASS2 {
  base_terrain                   GRASS
  number_of_clumps		15
  land_percent                   100
}


<ELEVATION_GENERATION>

if TINY_MAP
  create_elevation 6 {
    base_terrain GRASS2
    number_of_clumps 20
    number_of_tiles 2500
    set_scale_by_groups
    set_scale_by_size
  }
else
  create_elevation 6 {
    base_terrain GRASS2
    number_of_clumps 20
    number_of_tiles 5500
    set_scale_by_groups
    set_scale_by_size
  }
endif


<CLIFF_GENERATION>
min_number_of_cliffs 5
max_number_of_cliffs 8
min_length_of_cliff  2
max_length_of_cliff  6
min_distance_cliffs 12


<OBJECTS_GENERATION>

create_object TOWN_CENTER
{
     set_place_for_every_player
     group_placement_radius 18
     min_distance_to_players 0
     max_distance_to_players 0
}

create_object VILLAGER
{
     set_place_for_every_player
     min_distance_to_players 6
     max_distance_to_players 6
}

if REGICIDE
  create_object VILLAGER {
    number_of_objects 7
    set_place_for_every_player
    min_distance_to_players 6
    max_distance_to_players 6
  }
endif

create_object SCOUT {
  number_of_objects 1
  set_place_for_every_player
  min_distance_to_players 7
  max_distance_to_players 9
}

if REGICIDE
  create_object KING {
    set_place_for_every_player
    min_distance_to_players 6
    max_distance_to_players 6
  }

  create_object CASTLE {
    set_place_for_every_player
    min_distance_to_players 9
    max_distance_to_players 12
  }
endif

#const MAMALLAMA 305

start_random
  percent_chance 50 #define LLAMA_ACTION
end_random

if LLAMA_ACTION
  create_object MAMALLAMA
else
  create_object TURKEY
endif
{
  number_of_objects 4
  number_of_groups 1
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 9
  max_distance_to_players 11
}

if LLAMA_ACTION
  create_object MAMALLAMA
else
  create_object TURKEY
endif
{
  number_of_objects 2
  number_of_groups 2
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 14
  max_distance_to_players 24
}

create_object DEER {
  start_random
    percent_chance 50
      number_of_objects 4
    percent_chance 50
      number_of_objects 3
  end_random
  group_placement_radius 3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  start_random
    percent_chance 50
      min_distance_to_players 15
      max_distance_to_players 19
    percent_chance 50
      min_distance_to_players 25
      max_distance_to_players 30
  end_random
}

create_object JAVELINA {
     number_of_objects          1
     number_of_groups 		2
     set_loose_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players    16
     max_distance_to_players    22
}

create_object GOLD {
     number_of_objects 7
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 12
     max_distance_to_players 16
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object GOLD {
     number_of_objects 4
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 16
     max_distance_to_players 25
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object GOLD {
     number_of_objects 4
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 24
     max_distance_to_players 34
     min_distance_group_placement 6
     max_distance_to_other_zones 7
}

create_object STONE
{
     number_of_objects 4
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 14
     max_distance_to_players 18
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object STONE
{
     number_of_objects 4
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 17
     max_distance_to_players 24
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object FORAGE
{
  number_of_objects 6
  number_of_groups 1
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 10
  max_distance_to_players 13
  min_distance_group_placement 6
}

create_object JUNGLE_TREE
{
     number_of_objects             2
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players       4
     max_distance_to_players       5
     min_distance_group_placement  2
}

create_object JUNGLE_TREE
{
     number_of_objects             3
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players       6
     max_distance_to_players       8
     min_distance_group_placement  2
}

/* Far gold & stone */

create_object GOLD
{
if TINY_MAP
     number_of_groups           2
     number_of_objects          4
elseif SMALL_MAP
     number_of_groups           2
     number_of_objects          4
elseif MEDIUM_MAP
     number_of_groups           3
     number_of_objects          4
elseif LARGE_MAP
     number_of_groups           4
     number_of_objects          5
elseif HUGE_MAP
     number_of_groups           5
     number_of_objects          5
elseif GIGANTIC_MAP
     number_of_groups           6
     number_of_objects          6
else
     number_of_groups           11
     number_of_objects          6
endif
     group_placement_radius     2
     set_tight_grouping
     set_gaia_object_only
     min_distance_to_players       30
     min_distance_group_placement  10
}

create_object STONE
{
if TINY_MAP
     number_of_groups           2
     number_of_objects          3
elseif SMALL_MAP
     number_of_groups           2
     number_of_objects          3
elseif MEDIUM_MAP
     number_of_groups           3
     number_of_objects          4
elseif LARGE_MAP
     number_of_groups           4
     number_of_objects          4
elseif HUGE_MAP
     number_of_groups           5
     number_of_objects          4
elseif GIGANTIC_MAP
     number_of_groups           6
     number_of_objects          4
else
     number_of_groups           11
     number_of_objects          4
endif
     group_placement_radius     2
     set_tight_grouping
     set_gaia_object_only
     min_distance_to_players       30
     min_distance_group_placement  10
}

create_object SHORE_FISH
{
  number_of_objects                   100
  set_scaling_to_map_size
  set_gaia_object_only
 min_distance_group_placement  7
}

create_object RELIC {
  number_of_objects 5
  min_distance_to_players 25
  min_distance_group_placement 5
  temp_min_distance_group_placement 20
}

create_object JAGUAR {
  number_of_groups 3
  set_loose_grouping
  number_of_objects 1
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement 12
  min_distance_to_players 40
}

create_object JUNGLE_TREE {
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
