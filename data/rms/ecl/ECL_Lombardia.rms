/* Forgotten Empires - Lombardia (ver. October 13th) */

/* Note: random_map.def is automatically included in every rms script */


/* The Forgotten Fauna */

#const BRUCOUN 486
#const SUPAK 917
#const MLIKARNA 705
#const MAMALLAMA 305

/* The Forgotten Fauna End */


/* Sling nerf constants. */
#const TRIBUTE_INEFFICIENCY 46
#const BANKING 17
#const COINAGE 23
#const COINAGE_IMP_DISABLER 224


<PLAYER_SETUP>
grouped_by_team
ai_info_map_type ARABIA 0 0 0

/* Sling nerfs */
effect_percent MOD_RESOURCE TRIBUTE_INEFFICIENCY ATTR_SET 40
effect_amount DISABLE_TECH BANKING ATTR_DISABLE 17
effect_amount DISABLE_TECH COINAGE ATTR_DISABLE 23
effect_amount DISABLE_TECH COINAGE_IMP_DISABLER ATTR_DISABLE 224


<LAND_GENERATION>

base_terrain			GRASS

create_player_lands
{
  terrain_type			GRASS
  land_percent			22
  base_size			12
  border_fuzziness		12
}


<ELEVATION_GENERATION>

create_elevation 5 {
  base_terrain GRASS
  number_of_clumps 15
  number_of_tiles 3500
  set_scale_by_groups
  set_scale_by_size
}


<CLIFF_GENERATION>
min_number_of_cliffs 3
max_number_of_cliffs 7
min_length_of_cliff  2
max_length_of_cliff  7
min_distance_cliffs  15


<TERRAIN_GENERATION>

create_terrain GRASS_SNOW
{
  base_terrain GRASS
  number_of_clumps 10
  land_percent 46
  set_scale_by_groups
  spacing_to_other_terrain_types 2
}

create_terrain SNOW_FOREST
{
  base_terrain                   GRASS_SNOW
  spacing_to_other_terrain_types 4
  land_percent                   5
  number_of_clumps               5
  set_avoid_player_start_areas
  set_scale_by_groups
}

create_terrain PINE_FOREST
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 4
  land_percent                   7
  number_of_clumps               7
  set_avoid_player_start_areas
  set_scale_by_groups
}

create_terrain FOREST
{
  base_terrain                   PINE_FOREST
  number_of_clumps               40
  land_percent                   3
}

create_terrain GRASS
{
  base_terrain                   GRASS_SNOW
  number_of_clumps               5
  spacing_to_other_terrain_types 1
  land_percent                   1
  set_scale_by_groups
}


<OBJECTS_GENERATION>

create_object TOWN_CENTER
{
     set_place_for_every_player
     group_placement_radius 18
     min_distance_to_players 0
     max_distance_to_players 0
}

if REGICIDE
  create_object WATCH_TOWER
  {
    number_of_objects 1
    set_place_for_every_player
    min_distance_to_players 7
    max_distance_to_players 10
    min_distance_group_placement 3
  }
endif

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 6
}

if REGICIDE
  create_object VILLAGER
  {
    number_of_objects 7
    set_place_for_every_player
    min_distance_to_players 6
    max_distance_to_players 6
  }
endif

create_object SCOUT
{
  number_of_objects 1
  set_place_for_every_player
  min_distance_to_players 7
  max_distance_to_players 9
}

if REGICIDE
  create_object KING
  {
    set_place_for_every_player
    min_distance_to_players 6
    max_distance_to_players 6
  }
endif

create_object MLIKARNA
{
   number_of_objects 4
   number_of_groups 1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 9
   max_distance_to_players 11
}

create_object MLIKARNA
{
   number_of_objects 2
number_of_groups 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 14
   max_distance_to_players 24
}

create_object DEER
{
start_random
percent_chance 50
  number_of_objects 4
percent_chance 50
  number_of_objects 3
end_random
  group_placement_radius 3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
start_random
percent_chance 50
  min_distance_to_players 15
  max_distance_to_players 19
percent_chance 50
  min_distance_to_players 25
  max_distance_to_players 30
end_random
}

create_object BOAR
{
     number_of_objects          1
     number_of_groups 		1
     set_loose_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players    16
     max_distance_to_players    22
}


create_object BOAR
{
     number_of_objects          1
     number_of_groups 		1
     set_loose_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players    16
     max_distance_to_players    22
}


create_object GOLD
{
     number_of_objects 7
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 11
     max_distance_to_players 18
     min_distance_group_placement 3
     max_distance_to_other_zones 7
}

create_object GOLD
{
     number_of_objects 4
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 16
     max_distance_to_players 25
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object GOLD
{
     number_of_objects 4
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 24
     max_distance_to_players 34
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object STONE
{
     number_of_objects 4
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 12
     max_distance_to_players 18
     min_distance_group_placement 3
     max_distance_to_other_zones 7
}

create_object STONE
{
     number_of_objects 4
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 17
     max_distance_to_players 24
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object FORAGE
{
  number_of_objects 6
  number_of_groups 1
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 10
  max_distance_to_players 13
  min_distance_group_placement 3
}

create_object PINETREE
{
     number_of_objects             2
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players       4
     max_distance_to_players       5
     min_distance_group_placement  2
terrain_to_place_on GRASS
}

create_object PINETREE
{
     number_of_objects             3
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players       6
     max_distance_to_players       8
     min_distance_group_placement  2
terrain_to_place_on GRASS
}

create_object SNOWPINETREE
{
     number_of_objects             2
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players       4
     max_distance_to_players       5
     min_distance_group_placement  2
terrain_to_place_on GRASS_SNOW
}

create_object SNOWPINETREE
{
     number_of_objects             3
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players       6
     max_distance_to_players       8
     min_distance_group_placement  2
terrain_to_place_on GRASS_SNOW
}

/* Far gold & stone */

create_object GOLD
{
if TINY_MAP
     number_of_groups           2
     number_of_objects          4
elseif SMALL_MAP
     number_of_groups           2
     number_of_objects          4
elseif MEDIUM_MAP
     number_of_groups           3
     number_of_objects          4
elseif LARGE_MAP
     number_of_groups           4
     number_of_objects          5
elseif HUGE_MAP
     number_of_groups           5
     number_of_objects          5
elseif GIGANTIC_MAP
     number_of_groups           6
     number_of_objects          6
else
     number_of_groups           24
     number_of_objects          6
endif
     group_placement_radius     2
     set_tight_grouping
     set_gaia_object_only
     min_distance_to_players       30
     min_distance_group_placement  10
}

create_object STONE
{
if TINY_MAP
     number_of_groups           2
     number_of_objects          3
elseif SMALL_MAP
     number_of_groups           2
     number_of_objects          3
elseif MEDIUM_MAP
     number_of_groups           3
     number_of_objects          4
elseif LARGE_MAP
     number_of_groups           4
     number_of_objects          4
elseif HUGE_MAP
     number_of_groups           5
     number_of_objects          4
elseif GIGANTIC_MAP
     number_of_groups           6
     number_of_objects          4
else
     number_of_groups           24
     number_of_objects          4
endif
     group_placement_radius     2
     set_tight_grouping
     set_gaia_object_only
     min_distance_to_players       30
     min_distance_group_placement  10
}

create_object RELIC
{
  number_of_objects 5
  min_distance_to_players 25
  min_distance_group_placement 5
  temp_min_distance_group_placement 20
}

create_object BRUCOUN
{
     number_of_groups 3
     set_loose_grouping
        number_of_objects 1
  set_scaling_to_map_size
     set_gaia_object_only
     min_distance_group_placement 12
     min_distance_to_players 40
}

create_object PINETREE
{
  number_of_objects          12
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
terrain_to_place_on GRASS
}

create_object SNOWPINETREE
{
  number_of_objects          12
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
terrain_to_place_on GRASS_SNOW
}
