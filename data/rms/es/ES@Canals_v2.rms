/* ************* CANALS ****************** */
/* Sometime in November, 2000 */

/* ****************************************************** */
<PLAYER_SETUP>
  random_placement  /* note this is only currently valid entry */

/* ****************************************************** */

<LAND_GENERATION>
base_terrain PALM_DESERT

create_player_lands 
{
  terrain_type                     GRASS2
  land_percent                     8
  base_size                        9
}

create_land 
{
  terrain_type                     GRASS
  land_percent                   90
  left_border                    8
  top_border                     8
  right_border                   8
  bottom_border                  8
}


/* ****************************************************** */
<TERRAIN_GENERATION>

create_terrain PALM_DESERT
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 5
  land_percent                   5
  number_of_clumps               15
  set_scale_by_groups 
  set_avoid_player_start_areas     
}

create_terrain GRASS3
{
  base_terrain 			   GRASS
  spacing_to_other_terrain_types 1
  land_percent                   7
  number_of_clumps               9
  set_avoid_player_start_areas     
}

create_terrain LEAVES
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 1
  land_percent                   3
  number_of_clumps               26
}

create_terrain LEAVES
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 1
  land_percent                   1
  number_of_clumps               40
}

/* ****************************************************** */
<OBJECTS_GENERATION>

/* PLAYER START OBJECTS */

create_object TOWN_CENTER
{
  set_place_for_every_player
  group_placement_radius     18
  min_distance_to_players    0
  max_distance_to_players    0
}

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
 }

create_object SCOUT
{
  number_of_objects             1
  set_place_for_every_player
  min_distance_to_players       7
  max_distance_to_players       9
 }

create_object RELIC
{
   number_of_objects            5
   min_distance_to_players     25
   temp_min_distance_group_placement 20
}
 
/* SPECIAL STUFF FOR REGICIDE */
/* FORTRESS AND MIGRATION NEED TO BE UPDATED */

if REGICIDE
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object CASTLE
{
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    10
}

endif

/* NEAR FORAGE */

create_object FORAGE
{
  number_of_objects          6
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    12
  min_distance_group_placement  6
}

/* NEAR GOLD */

create_object GOLD
{
  number_of_objects          7
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    12
  max_distance_to_players    16
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* MEDIUM GOLD */

create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    18
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR GOLD */

create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    25
  max_distance_to_players    35
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* NEAR STONE */

create_object STONE
{
  number_of_objects          5
  group_placement_radius     2  
  set_tight_grouping 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    14
  max_distance_to_players    18
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR STONE */

create_object STONE
{
  number_of_objects          4
  group_placement_radius     2  
  set_tight_grouping 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    20
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

create_object DEER
{
  number_of_objects          4
  group_variance             1
  group_placement_radius     3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    14
  max_distance_to_players    30
}

if TROPICAL_MAP
create_object JAVELINA
else
create_object BOAR
endif
{
  number_of_objects          1
  set_loose_grouping  
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    16
  max_distance_to_players    22
}

if TROPICAL_MAP
create_object JAVELINA
else
create_object BOAR
endif
{
  number_of_objects          1
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    16
  max_distance_to_players    22
}

if TROPICAL_MAP
create_object JAGUAR
{
  number_of_objects          2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    30
  max_distance_to_players    40
}
else
create_object WOLF
{
  number_of_objects          2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    30
  max_distance_to_players    40
}
endif

/* *****RESOURCES BY MAP***** */

if LARGE_MAP
create_object FORAGE
{
  number_of_groups           1    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object FORAGE
{
  number_of_groups           2    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object FORAGE
{
  number_of_groups           3    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

/* EXTRA GOLD */

if TINY_MAP
create_object GOLD
{
  number_of_groups           2    
  number_of_objects          3
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object GOLD
{
  number_of_groups           2    
  number_of_objects          3
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          3
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          3
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object GOLD
{
  number_of_groups           4    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object GOLD
{
  number_of_groups           5    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

/* EXTRA STONE */

if TINY_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object STONE
{
  number_of_groups           2    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object STONE
{
  number_of_groups           3    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object STONE
{
  number_of_groups           4    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object STONE
{
  number_of_groups           5    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

create_object PALMTREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object PALMTREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}

create_object MACAW
{
   number_of_objects         6
   set_scaling_to_map_size
}

create_object WOLF
{
  number_of_groups          3
  set_loose_grouping
  start_random
  percent_chance 75
	number_of_objects 1
  percent_chance 25
	number_of_objects 2
  end_random
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement 12
  min_distance_to_players    40
  max_distance_to_players    120
} 


   create_object BOAR
{
   number_of_objects 3
   group_variance    1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    35
   min_distance_group_placement  5
}

   create_object PALMTREE
{
  number_of_objects          100
  set_gaia_object_only
  min_distance_to_players       6
  min_distance_group_placement  2
}

create_object DORADO
{
  number_of_objects                   10
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         2
}
create_object TUNA
{
  number_of_objects                   9
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         2
}
create_object SNAPPER
{
  number_of_objects                   9
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         2
} 

create_object SHORE_FISH
{
  number_of_objects                   30
  set_scaling_to_map_size
  min_distance_group_placement        3
  set_gaia_object_only
}

create_object MARLIN1
{
  number_of_groups                    5
  number_of_objects                    1
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement        10
  max_distance_to_other_zones         2
} 

create_object MARLIN2
{
  number_of_groups                    5
  number_of_objects                    1
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement        10
  max_distance_to_other_zones         2
} 

<CONNECTION_GENERATION>

create_connect_all_players_land 
{
  replace_terrain GRASS         WATER
  replace_terrain GRASS3        WATER
  replace_terrain LEAVES        WATER 
  replace_terrain WATER         WATER
  replace_terrain BEACH         WATER    
  replace_terrain PALM_DESERT   WATER 
  terrain_cost    GRASS         4
  terrain_cost    GRASS3        4
  terrain_cost    LEAVES        4
  terrain_cost    WATER         4
  terrain_cost    BEACH         4
  terrain_cost    PALM_DESERT   8
  terrain_size    GRASS         1       0
  terrain_size    GRASS3        1       0
  terrain_size    LEAVES        1       0
  terrain_size    WATER         2       1
  terrain_size    BEACH         1       0
  terrain_size    PALM_DESERT   1       0
}



