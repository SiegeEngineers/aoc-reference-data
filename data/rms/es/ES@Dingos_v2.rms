/* ************ DINGOS ************ */
/* 4 OCT 00 */

/* ****************************************************** */
<PLAYER_SETUP>
  random_placement  /* note this is only currently valid entry */

/* ****************************************************** */
<LAND_GENERATION>

base_terrain WATER

start_random
  percent_chance 20
  #define ASIAN_MAP
  percent_chance 20
  #define ALPINE_MAP
end_random

/* Fill in center */

create_land
{            
if ASIAN_MAP
  terrain_type                     GRASS2
elseif ALPINE_MAP
  terrain_type                     GRASS2
else           
  terrain_type                     GRASS
endif
land_percent                       10
base_size 7
left_border                   25
right_border                  25
top_border                    25
bottom_border                 25
land_id				4 
}

create_player_lands 
{            
if ASIAN_MAP
  terrain_type                     GRASS2
elseif ALPINE_MAP
  terrain_type                     GRASS2
else           
  terrain_type                     GRASS
endif
land_percent                       50
base_size                           9
left_border                         6
right_border                        6
top_border                          6
bottom_border                       6
border_fuzziness                 10
set_zone_randomly  
other_zone_avoidance_distance    5
}

/* ****************************************************** */
<TERRAIN_GENERATION>

/* MIXING WATER */

create_terrain MED_WATER
{
base_terrain WATER
number_of_clumps               3
spacing_to_other_terrain_types 2
land_percent 40
}

create_terrain MED_WATER
{
base_terrain WATER
number_of_clumps               30
spacing_to_other_terrain_types 1
land_percent 1
}

create_terrain DEEP_WATER
{
base_terrain MED_WATER
number_of_clumps               5
spacing_to_other_terrain_types 5
land_percent 20
}

create_terrain DEEP_WATER
{
base_terrain MED_WATER
number_of_clumps               30
spacing_to_other_terrain_types 1
land_percent 1
}

create_terrain MED_WATER
{
base_terrain DEEP_WATER
number_of_clumps               30
spacing_to_other_terrain_types 1
land_percent 1
}

create_terrain WATER
{
base_terrain MED_WATER
number_of_clumps               30
spacing_to_other_terrain_types 1
land_percent 1
}

create_terrain MED_WATER
{
base_terrain WATER
number_of_clumps               30
spacing_to_other_terrain_types 1
land_percent 1
}

/* PRIMARY FOREST */

if ASIAN_MAP
  create_terrain PINE_FOREST
{
  base_terrain                   GRASS2
  spacing_to_other_terrain_types 5
  land_percent                   7
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif ALPINE_MAP
create_terrain PINE_FOREST
{
  base_terrain                   GRASS2
  spacing_to_other_terrain_types 5
  land_percent                   7
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain FOREST
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 5
  land_percent                   7
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

/* PRIMARY PATCH */

if ASIAN_MAP
   create_terrain GRASS3
{
  base_terrain                   GRASS2
  number_of_clumps               8
  spacing_to_other_terrain_types 0
  land_percent                   4
  set_scale_by_size
}
elseif ALPINE_MAP
create_terrain GRASS3
{
  base_terrain                   GRASS2
  number_of_clumps               8
  spacing_to_other_terrain_types 0
  land_percent                   6
  set_scale_by_size
}
else
create_terrain DIRT
{
  base_terrain                   GRASS
  number_of_clumps               8
  spacing_to_other_terrain_types 0
  land_percent                   9
  set_scale_by_size
}
endif

/* SECONDARY FOREST */

if ASIAN_MAP
create_terrain BAMBOO
{
base_terrain                   GRASS3

  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif ALPINE_MAP
create_terrain FOREST
{
base_terrain                   GRASS3

  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain PALM_DESERT
{
  base_terrain                   DIRT
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

/* SECONDARY PATCH */

if ASIAN_MAP
   create_terrain DIRT3
{
  base_terrain                   GRASS2
  number_of_clumps               24
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
elseif ALPINE_MAP
create_terrain DIRT3
{
  base_terrain                   GRASS2
  number_of_clumps               24
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
else
create_terrain GRASS3
{
  base_terrain                   GRASS
  number_of_clumps               24
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
endif

/* TERTIARY PATCH */

if ASIAN_MAP
   create_terrain GRASS
{
  base_terrain                   GRASS2
  number_of_clumps               30
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
elseif ALPINE_MAP
create_terrain GRASS
{
  base_terrain                   GRASS2
  number_of_clumps               30
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
else
create_terrain DIRT3
{
  base_terrain                   GRASS
  number_of_clumps               30
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
endif

/* ****************************************************** */
<OBJECTS_GENERATION>

/* PLAYER START OBJECTS */

create_object TOWN_CENTER
{
  set_place_for_every_player
  group_placement_radius     18
  min_distance_to_players    0
  max_distance_to_players    0
}

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
 }

create_object RELIC
{
   number_of_objects            5
   min_distance_to_players     25
   temp_min_distance_group_placement 20
}
 
/* SPECIAL STUFF FOR REGICIDE */
/* FORTRESS AND MIGRATION NEED TO BE UPDATED */

if REGICIDE
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object CASTLE
{
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    10
}

endif

/* NEAR GOLD */

create_object GOLD
{
  number_of_objects          7
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    12
  max_distance_to_players    16
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* MEDIUM GOLD */

create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    18
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR GOLD */

create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    25
  max_distance_to_players    35
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* NEAR STONE */

create_object STONE
{
  number_of_objects          5
  group_placement_radius     2  
  set_tight_grouping 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    14
  max_distance_to_players    18
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR STONE */

create_object STONE
{
  number_of_objects          4
  group_placement_radius     2  
  set_tight_grouping 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    20
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* BAA BAA */

if TROPICAL_MAP
create_object TURKEY
{
   number_of_objects 4
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    10
   max_distance_to_players    12
}
create_object TURKEY
{
   number_of_objects 2
   number_of_groups  5
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    14
   max_distance_to_players    30
}
else
create_object SHEEP
{
   number_of_objects 4
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    10
   max_distance_to_players    12
}
create_object SHEEP
{
   number_of_objects 2
   number_of_groups  5
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    14
   max_distance_to_players    30
}
endif

if TROPICAL_MAP
create_object JAGUAR
{
  number_of_objects          3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    30
  max_distance_to_players    40
}
else
create_object WOLF
{
  number_of_objects          3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    30
  max_distance_to_players    40
}
endif

/* *****RESOURCES BY MAP***** */

/* EXTRA GOLD */

if TINY_MAP
create_object GOLD
{
  number_of_groups           2    
  number_of_objects          3
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object GOLD
{
  number_of_groups           2    
  number_of_objects          3
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          3
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          3
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object GOLD
{
  number_of_groups           4    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object GOLD
{
  number_of_groups           5    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

/* EXTRA STONE */

if TINY_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object STONE
{
  number_of_groups           2    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object STONE
{
  number_of_groups           3    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object STONE
{
  number_of_groups           4    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object STONE
{
  number_of_groups           5    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if DESERT_MAP
create_object PALMTREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object PALMTREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
elseif ALPINE_MAP
create_object PINETREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object PINETREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
elseif ASIAN_MAP
create_object BAMBOO_TREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object PINETREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
elseif TROPICAL_MAP
create_object PALMTREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object OAKTREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
elseif FROZEN_MAP
create_object SNOWPINETREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object SNOWPINETREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
else
create_object OAKTREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object OAKTREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
endif

if TROPICAL_MAP
create_object MACAW
{
   number_of_objects         6
   set_scaling_to_map_size
}
else
create_object HAWK
{
   number_of_objects         6
   set_scaling_to_map_size
}
endif

if TROPICAL_MAP
create_object JAGUAR
{
  number_of_groups          4
  set_loose_grouping
  start_random
  percent_chance 75
	number_of_objects 1
  percent_chance 25
	number_of_objects 2
  end_random
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement 12
  min_distance_to_players    40
  max_distance_to_players    120
} 
else
create_object WOLF
{
  number_of_groups          4
  set_loose_grouping
  start_random
  percent_chance 75
	number_of_objects 1
  percent_chance 25
	number_of_objects 2
  end_random
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement 12
  min_distance_to_players    40
  max_distance_to_players    120
} 
endif

if TROPICAL_MAP
create_object TURKEY
{
   number_of_objects 4
   number_of_groups   4   
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    19
   }
else
create_object SHEEP
{
   number_of_objects 4
   number_of_groups   4   
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    19
   }
endif

if ASIAN_MAP
create_object BAMBOO_TREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
elseif ALPINE_MAP
create_object PINETREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
else
create_object OAKTREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
endif

create_object SKELETON
{
  number_of_objects               20
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players          8
  place_on_specific_land_id        4
}

create_object FISH
{
  number_of_objects                   25
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
} 

if ALPINE_MAP
create_object SALMON
{
  number_of_objects                   15
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
}
create_object SNAPPER
{
  number_of_objects                   10
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
} 
elseif ASIAN_MAP
create_object TUNA
{
  number_of_objects                   15
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
}
create_object SNAPPER
{
  number_of_objects                   10
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
} 
else
create_object DORADO
{
  number_of_objects                   5
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
}
create_object TUNA
{
  number_of_objects                   10
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
}
create_object SNAPPER
{
  number_of_objects                   10
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
} 
endif

create_object SHORE_FISH
{
  number_of_objects                   25
  set_scaling_to_map_size
  min_distance_group_placement         3
  set_gaia_object_only
}

create_object MARLIN1
{
  number_of_groups                      5
  number_of_objects                     1
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement        10
  max_distance_to_other_zones         7
} 

create_object MARLIN2
{
  number_of_groups                      5
  number_of_objects                     1
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement        10
  max_distance_to_other_zones         7
} 

/* ****************************************************** */

<ELEVATION_GENERATION>

create_elevation        7
{
if ALPINE_MAP
  base_terrain                GRASS2
elseif ASIAN_MAP
  base_terrain                GRASS2
else           
  base_terrain                GRASS
endif
  number_of_clumps 		14
  number_of_tiles 		2000
  set_scale_by_groups
  set_scale_by_size           
}

<CLIFF_GENERATION>

min_number_of_cliffs 5
max_number_of_cliffs 8
min_length_of_cliff  4
max_length_of_cliff  10
cliff_curliness      10
min_distance_cliffs  3     


/* ****************************************************** */
<CONNECTION_GENERATION>
create_connect_all_players_land
{
  replace_terrain WATER         SHALLOW
  replace_terrain MED_WATER         SHALLOW
  replace_terrain DEEP_WATER         SHALLOW
  terrain_cost    WATER         7
  terrain_cost    MED_WATER     9
  terrain_cost    DEEP_WATER    15
  terrain_cost    FOREST        7
  terrain_cost    BAMBOO        7
  terrain_cost    PALM_DESERT   7
  terrain_cost    PINE_FOREST   7
  terrain_cost    SHALLOW       3
  terrain_cost    DESERT        1
  terrain_cost    DIRT2         2
  terrain_cost    DIRT3         3
  terrain_cost    GRASS         2
  terrain_cost    GRASS2        2
  terrain_cost    GRASS3        2
  terrain_cost    BEACH         7
  terrain_size    WATER         3       1
  terrain_size    MED_WATER     3       1
  terrain_size    DEEP_WATER    3       1
  terrain_size    GRASS2        0       0
  terrain_size    GRASS3        0       0
  terrain_size    GRASS         0       0
  terrain_size    FOREST        0       0
  terrain_size    BAMBOO        0       0
  terrain_size    PINE_FOREST   0       0
  terrain_size    PALM_DESERT   0       0
  terrain_size    DESERT        0       0
  terrain_size    DIRT2         0       0
  terrain_size    DIRT3         0       0
}
